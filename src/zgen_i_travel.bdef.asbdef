managed;
strict ( 2 );
with draft;

define behavior for ZGEN_I_TRAVEL alias Travel
implementation in class zbp_gen_i_travel unique
persistent table zgen_travel
draft table zgen_d_travel
lock master
total etag LastChangedAt
authorization master ( global, instance )

etag master LocalLastChangedAt
{
  create(precheck);
  update(precheck);
  delete(precheck);
  field ( numbering : managed, readonly ) TravelUuid;
  field ( readonly ) TravelId, OverallStatus, TotalPrice, LocalCreatedAt, LocalCreatedBy, LocalLastChangedAt, LocalLastChangedBy, LastChangedAt;
  field ( mandatory ) CustomerID, AgencyID, BeginDate, EndDate, CurrencyCode;
  field ( features : instance ) BookingFee;

  association _Booking { create ( features : instance ); with draft; }





  determination setTravelID on save { create; }
  determination setStaustoOpen on modify { create; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }


  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDates on save { create; update; field BeginDate, EndDate; }


  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  action ( features : instance ) deductDiscount parameter ZGEN_A_DISCOUNT result [1] $self { default function GetDefaultsFordeductDiscounts; }
  internal action reCalcTotalProce;

  draft action Resume;
  draft action Edit;
  draft action Discard;
  draft action Activate optimized;
  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateAgency;
    validation validateDates;

  }

  determine action validateCustomerID { validation validateCustomer; }
  determine action validateAgencyID { validation validateAgency; }
  determine action validateDatesVal { validation validateDates; }
  side effects
  {
    determine action validateCustomerID executed on field CustomerId affects messages;
    determine action validateAgencyID executed on field AgencyId affects messages;
    determine action validateDatesVal executed on field BeginDate, field EndDate affects messages;
    field BookingFee affects field TotalPrice;
    //    determine action Prepare executed on $self affects messages;
  }
  mapping for zgen_travel
    {
      AgencyID           = agency_id;
      BeginDate          = begin_date;
      BookingFee         = booking_fee;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      Description        = description;
      EndDate            = end_date;
      LocalCreatedAt     = local_created_at;
      LocalCreatedBy     = local_created_by;
      LocalLastChangedAt = local_last_changed_at;
      LocalLastChangedBy = local_last_changed_by;
      OverallStatus      = overall_status;
      TotalPrice         = total_price;
      TravelID           = travel_id;
      TravelUUID         = travel_uuid;
      LastChangedAt      = last_changed_at;
    }

}

define behavior for ZGEN_I_BOOKING alias Booking
implementation in class zbp_gen_i_Booking unique
persistent table zgen_booking
draft table zgen_d_booking
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookingUuid;
  field ( readonly ) TravelUuid, BookingId, BookingDate, LocalLastChangedAt;
  association _Travel { with draft; }
  association _Bksuppl { create; with draft; }


  determination setBookingId on save { create; }
  determination setBookingDate on save { create; }
  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  side effects
  {

    field FlightPrice affects field _Travel.TotalPrice;
  }

  mapping for zgen_booking
    {
      CarrierId          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }
}

define behavior for ZGEN_I_BKSUPPL alias BokkingSupplement
implementation in class zbp_gen_i_bksuppl unique
persistent table zgen_bksuppl
draft table zgen_d_bksuppl
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BooksupplUuid;
  field ( readonly ) TravelUuid, BookingUuid, BookingSupplementId;
  association _Travel { with draft; }
  association _Booking { with draft; }

  determination setBookingSupplId on save { create; }
  determination calculateTotalPrice on modify { create; field Price, CurrencyCode; }

  side effects
  {

    field Price affects field _Travel.TotalPrice;
  }


  mapping for zgen_bksuppl
    {
      Price               = price;
      BookSupplUUID       = booksuppl_uuid;
      BookingSupplementID = booking_supplement_id;
      BookingUUID         = parent_uuid;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
      SupplementID        = supplement_id;
      TravelUUID          = root_uuid;
    }
}